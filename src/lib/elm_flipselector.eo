class Elm.Flipselector (Elm.Layout, Elm_Interface_Atspi_Widget_Action,
                        Evas.Selectable_Interface)
{
   eo_prefix: elm_obj_flipselector;
   methods {
      @property first_interval {
         set {
            [[Set the interval on time updates for a user mouse button hold
              on a flip selector widget.

              This interval value is decreased while the user holds the
              mouse pointer either flipping up or flipping down a given flip
              selector.

              This helps the user to get to a given item distant from the
              current one easier/faster, as it will start to flip quicker and
              quicker on mouse button holds.

              The calculation for the next flip interval value, starting from
              the one set with this call, is the previous interval divided by
              1.05, so it decreases a little bit.

              The default starting interval value for automatic flips is
              0.85 seconds.

              See also @.first_interval.get.
            ]]
         }
         get {
            [[Get the interval on time updates for an user mouse button hold
              on a flip selector widget.

              See also @.first_interval.set for more details.
            ]]
         }
         values {
            interval: double; [[The (first) interval value in seconds.]]
         }
      }
      @property items {
         get {
            [[Get the internal list of items in a given flip selector widget.

              This list is not to be modified in any way and must not be
              freed. Use the list members with functions like
              \@ref elm_object_item_text_set,
              \@ref elm_object_item_text_get,
              \@ref elm_object_item_del,
              \@ref elm_flipselector_item_selected_get,
              \@ref elm_flipselector_item_selected_set.

              Warning: This list is only valid until $obj object's internal
              items list is changed. It should be fetched again with another
              call to this function when changes happen.
            ]]
            return: const(list<Elm.Object.Item*>)*;
         }
      }
      @property first_item {
         get {
            [[Get the first item in the given flip selector widget's list of
              items.

              See also @.item_append,
              @.last_item.get.
            ]]
            return: Elm.Widget_Item *; [[The first item or $null, if it has no items (and on
            errors).]]

         }
      }
      @property last_item {
         get {
            [[Get the last item in the given flip selector widget's list of
              items.

              See also @.item_prepend,
              @.first_item.get.
            ]]
            return: Elm.Widget_Item *; [[The last item or $null, if it has no items (and on
            errors).]]

         }
      }
      @property selected_item {
         get {
            [[Get the currently selected item in a flip selector widget.]]
            return: Elm.Widget_Item *; [[The selected item or $null, if the widget has no items
              (and on errors).]]

         }
      }
      item_prepend {
         [[Prepend a (text) item to a flip selector widget

           The widget's list of labels to show will be prepended with the
           given value. If the user wishes so, a callback function pointer
           can be passed, which will get called when this same item is
           selected.

           Note: The current selection won't be modified by prepending
           an element to the list.

           Note: The maximum length of the text label is going to be
           determined by the widget's theme. Strings larger than
           that value are going to be truncated.
         ]]
         return: Elm.Widget_Item *;
         params {
            @in label: const(char)*; [[The (text) label of the new item.]]
            @in func: Evas_Smart_Cb @optional; [[Convenience callback function to take place when
            item is selected.]]
            @in data: void * @optional; [[Data passed to $func, above.]]
         }
      }
      flip_next {
         [[Programmatically select the next item of a flip selector widget

           Note: The selection will be animated. Also, if it reaches the
           end of its list of member items, it will continue with the first
           one onwards.
         ]]
      }
      item_append {
         [[Append a (text) item to a flip selector widget

           The widget's list of labels to show will be appended with the
           given value. If the user wishes so, a callback function pointer
           can be passed, which will get called when this same item is
           selected.

           Note: The current selection won't be modified by appending an
           element to the list.

           Note: The maximum length of the text label is going to be
           determined by the widget's theme. Strings larger than
           that value are going to be truncated.
         ]]
         return: Elm.Widget_Item *;
         params {
            @in label: const(char)*; [[The (text) label of the new item.]]
            @in func: Evas_Smart_Cb @optional; [[Convenience callback function to take place when
            item is selected.]]
            @in data: const(void)* @optional; [[Data passed to $func, above.]]
         }
      }
      flip_prev {
         [[Programmatically select the previous item of a flip selector
           widget

           Note: The selection will be animated. Also, if it reaches the
           beginning of its list of member items, it will continue with the
           last one backwards.
         ]]
      }
   }
   implements {
      class.constructor;
      Eo.Base.constructor;
      Evas.Object_Smart.add;
      Evas.Object_Smart.del;
      Elm.Widget.theme_apply;
      Elm.Widget.focus_next_manager_is;
      Elm.Widget.focus_direction_manager_is;
      Elm.Widget.event;
      Elm.Layout.sizing_eval;
      Elm_Interface_Atspi_Widget_Action.elm_actions.get;
   }
   events {
      underflowed;
      overflowed;
      language,changed;
      access,changed;
   }

}
